pragma ComponentBehavior: Bound
import QtQuick
import QtQuick.Controls
import io.lecooctrl.gui

// Material 3 linear progress indicator.
ProgressBar {
    id: root
    property real valueBarWidth: 120
    property real valueBarHeight: 4
    property real valueBarGap: 4
    property color highlightColor: Appearance.colors.colPrimary
    property color trackColor: Appearance.m3colors.m3secondaryContainer
    property bool wavy: false
    property bool animateWave: true
    property real waveAmplitudeMultiplier: wavy ? 0.5 : 0
    property real waveFrequency: 6

    Behavior on waveAmplitudeMultiplier {
        animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
    }

    Behavior on value {
        animation: Appearance.animation.elementMove.numberAnimation.createObject(this)
    }

    background: Item {
        implicitHeight: valueBarHeight
        implicitWidth: valueBarWidth
    }

    contentItem: Item {
        id: contentItem
        anchors.fill: parent

        Loader {
            anchors {
                left: parent.left
                verticalCenter: parent.verticalCenter
            }
            active: root.wavy
            sourceComponent: WavyLine {
                id: wavyFill
                frequency: root.waveFrequency
                color: root.highlightColor
                amplitudeMultiplier: root.wavy ? 0.5 : 0
                height: contentItem.height * 6
                width: contentItem.width * root.visualPosition
                lineWidth: contentItem.height
                fullLength: root.width
                Connections {
                    target: root
                    function onValueChanged() {
                        wavyFill.requestPaint();
                    }
                    function onHighlightColorChanged() {
                        wavyFill.requestPaint();
                    }
                }
                FrameAnimation {
                    running: root.animateWave
                    onTriggered: wavyFill.requestPaint()
                }
            }
        }

        Loader {
            active: !root.wavy
            sourceComponent: Rectangle {
                anchors.left: parent.left
                width: contentItem.width * root.visualPosition
                height: contentItem.height
                radius: height / 2
                color: root.highlightColor
            }
        }

        Rectangle {
            anchors.right: parent.right
            width: Math.max(0, (1 - root.visualPosition) * parent.width)
            height: parent.height
            radius: height / 2
            color: root.trackColor
        }
    }
}
